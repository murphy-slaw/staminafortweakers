import org.jetbrains.changelog.Changelog

plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}
repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://repo.repsy.io/mvn/lucraft/threetag/"}
    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    modCompileOnly("maven.modrinth:gliders:${gliders_version}")
    modCompileOnly("net.threetag:PalladiumCore:${palladium_core_version}")
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("com.terraformersmc:modmenu:${modmenu_version}")
    modImplementation include("maven.modrinth:AdditionalEntityAttributes:${aea_version}")
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
    modCompileOnly("maven.modrinth:better-combat:${bettercombat_version}-fabric")
    modCompileOnly("maven.modrinth:playeranimator:${playeranimator_version}-fabric")
    api("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    type = STABLE
    modLoaders.add("fabric")
    version = "${rootProject.version}+${minecraft_version}"
    displayName = "Stamina for Tweakers ${rootProject.version}+${minecraft_version}"
    changelog = project.changelog.renderItem(project.changelog
            .getLatest()
            .withHeader(false)
            .withEmptySections(false),
            Changelog.OutputType.MARKDOWN
    )

    curseforge {
        projectId = "1040808"
        projectSlug = "stamina-for-tweakers" // Required for discord webhook
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(project.minecraft_version)
        javaVersions.add(JavaVersion.VERSION_21)
        clientRequired = true
        serverRequired = true
        requires("fabric-api")
        requires("cloth-config")
        optional("modmenu")
    }

    modrinth {
        projectId = "AejtfAlC"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(project.minecraft_version)
        requires("fabric-api")
        requires("cloth-config")
        requires("cardinal-components-api")
        optional("modmenu")
    }
}
