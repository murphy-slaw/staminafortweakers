import org.jetbrains.changelog.Changelog

plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}
repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://repo.repsy.io/mvn/lucraft/threetag/"}
    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
    modCompileOnly("maven.modrinth:gliders:${gliders_version}")
    modCompileOnly("net.threetag:PalladiumCore:${palladium_core_version}")
    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("com.terraformersmc:modmenu:${modmenu_version}")
    modImplementation include("maven.modrinth:AdditionalEntityAttributes:${aea_version}")
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
    modImplementation("maven.modrinth:better-combat:1.8.6+1.20.1-fabric")
    modImplementation("maven.modrinth:playeranimator:1.0.2-rc1+1.20-fabric")
    api("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
}

loom {

    //splitEnvironmentSourceSets()

    mods {
        "staminafortweakers" {
            sourceSet sourceSets.main
            //sourceSet sourceSets.client
        }
    }
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

publishMods {
    type = STABLE
    version = "${rootProject.version}+${minecraft_version}"
    displayName = "Stamina for Tweakers ${rootProject.version}+${minecraft_version}"
    file = remapJar.archiveFile
    changelog = project(":common").changelog.renderItem(project(":common").changelog
            .getLatest()
            .withHeader(false)
            .withEmptySections(false),
            Changelog.OutputType.MARKDOWN
    )

    curseforge("curseforgeFabric") {
        projectId = "1040808"
        projectSlug = "stamina-for-tweakers" // Required for discord webhook
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.add(minecraft_version)
        javaVersions.add(JavaVersion.VERSION_17)
        clientRequired = true
        serverRequired = true
        modLoaders.add("fabric")
        requires("cloth-config")
        optional("modmenu")
    }


    def mrOptions = modrinthOptions {
        projectId = "AejtfAlC"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(minecraft_version)
        requires("cloth-config")
    }

    modrinth("modrinthFabric") {
        from mrOptions
        modLoaders.add("fabric")
    }
}